resource "aws_kms_key" "enc_dec" {
  description              = "The key used to encrypt and decrypt through s3 app resources."
  key_usage                = "ENCRYPT_DECRYPT"
  customer_master_key_spec = "SYMMETRIC_DEFAULT"
  multi_region             = true
  enable_key_rotation      = false
  deletion_window_in_days  = local.key_deletion_window_in_days
  policy                   = data.aws_iam_policy_document.use_enc_dec_key_policy.json
}

resource "aws_kms_alias" "enc_dec" {
  name          = "alias/${local.env}-enc-dec-key"
  target_key_id = aws_kms_key.enc_dec.key_id
}

# ------------------------------------------------------------#
#  data
# ------------------------------------------------------------#

data "aws_iam_policy_document" "use_enc_dec_key_policy" {
  statement {
    sid    = "Enable IAM User Permissions"
    effect = "Allow"

    principals {
      type        = "AWS"
      identifiers = ["arn:aws:iam::${data.aws_caller_identity.current.account_id}:root"]
    }

    actions   = ["kms:*"]
    resources = ["*"]
  }
  
  statement {
    sid    = "Allow to use enc_dec key"
    effect = "Allow"

    principals {
      type        = "Service"
      identifiers = ["logs.ap-northeast-1.amazonaws.com"]
    }

    actions = [
      "kms:Encrypt*",
      "kms:Decrypt*",
      "kms:ReEncrypt*",
      "kms:GenerateDataKey*",
      "kms:Describe*",
    ]

    resources = ["*"]

    condition {
      test     = "ArnLike"
      variable = "kms:EncryptionContext:aws:logs:arn"
      values   = ["arn:aws:logs:${data.aws_region.current.name}:${data.aws_caller_identity.current.account_id}:*"]
    }
  }
}